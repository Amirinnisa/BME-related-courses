real L=12;
real ratio=L;
border floor(t=0,L){ x=t; y=0; label=1;};
border right(t=0,2){ x=L; y=t; label=2;};
border ceilingright(t=L,8){ x=t; y=2; label=4;};
border halfcircle(t=0,1*pi) { x=2*cos(t)+6; y=1*sin(t)+2;label=4;};
border ceilingleft(t=4,0){ x=t; y=2; label=4;};
border left(t=2,0){ x=0; y=t; label=3;};
int n=5
;
mesh Th= buildmesh(floor(ratio*n)+right(n)+ceilingright(ratio*n)+halfcircle(ratio*n)+ceilingleft(ratio*n)+left(n));
plot(Th,wait=1);

fespace Xh(Th,P2);
fespace Mh(Th,P1);
Xh u2,v2;
Xh u1,v1;
Mh p,q;

//solve stokes problem for initial value of u
solve Stokes ([u1,u2,p],[v1,v2,q],solver=Crout) =
int2d(Th)( ( dx(u1)*dx(v1) + dy(u1)*dy(v1)
+ dx(u2)*dx(v2) + dy(u2)*dy(v2) )
+ p*q*(0.000001)
+ p*dx(v1)+ p*dy(v2)
+ dx(u1)*q+ dy(u2)*q
)
+ on(3,u1=1,u2=0)
+ on(1,4,u1=0,u2=0);
Xh psi,phi;

solve streamlines(psi,phi) =
int2d(Th)( dx(psi)*dx(phi) + dy(psi)*dy(phi))
+ int2d(Th)( -phi*(dy(u1)-dx(u2)))
+ on(1,psi=0);
plot(cmm="psi", psi,value=true,wait=1,fill=1);

int i=0;
real nu=1./100.; //reynold = 100
real dt=0.1;
real alpha=1/dt;
Xh up1,up2;
problem NS ([u1,u2,p],[v1,v2,q],solver=Crout,init=i) =
int2d(Th)(
alpha*( u1*v1 + u2*v2)
+ nu * ( dx(u1)*dx(v1) + dy(u1)*dy(v1)
+ dx(u2)*dx(v2) + dy(u2)*dy(v2) )
+ p*q*(0.000001)
+ p*dx(v1)+ p*dy(v2)
+ dx(u1)*q+ dy(u2)*q
)
+ int2d(Th) ( -alpha*convect([up1,up2],-dt,up1)*v1 -
alpha*convect([up1,up2],-dt,up2)*v2 )
+ on(3,u1=1,u2=0)
+ on(1,4,u1=0,u2=0)
;
for (i=0;i<=125;i++)
{
up1=u1;
up2=u2;
NS;
streamlines;
plot(cmm=i,psi,value=true);
} ;
plot(cmm="u1",value=true,u1,wait=1,fill=1);
plot(cmm="u2",value=true,u2,wait=1,fill=1);
plot(coef=0.1,cmm="u1, u2",[u1,u2],value=true,wait=1);
plot(cmm="p", p,value=true,wait=1,fill=1);